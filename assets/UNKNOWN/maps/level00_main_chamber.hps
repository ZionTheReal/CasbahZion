////////////////////
//START BEGIN GAME//
void StartGame(string &in asTimer)
{
	FadeGlobalSoundVolume(1, 3);
	FadeGlobalSoundSpeed(1, 3);
	AddTimer("GateOpen", 3, "GateOpen");
}

void GateOpen(string &in asTimer)
{
	PlayGuiSound("door_level_wood_open.snt", 1.0f);
	AddTimer("Footstep1", 3, "Footstep1");
}

void Footstep1(string &in asTimer)
{
	PlayGuiSound("step_walk_rock.snt", 1.0f);
	AddTimer("Footstep2", 0.5f, "Footstep2");
}

void Footstep2(string &in asTimer)
{
	PlayGuiSound("step_walk_rock.snt", 1.0f);
	AddTimer("Footstep3", 0.5f, "Footstep3");
}

void Footstep3(string &in asTimer)
{
	PlayGuiSound("step_walk_rock.snt", 1.0f);
	AddTimer("Footstep4", 0.5f, "Footstep4");
}

void Footstep4(string &in asTimer)
{
	PlayGuiSound("step_walk_rock.snt", 1.0f);
	AddTimer("Footstep5", 0.5f, "Footstep5");
}

void Footstep5(string &in asTimer)
{
	PlayGuiSound("step_walk_rock.snt", 1.0f);
	AddTimer("CloseGate", 1, "CloseGate");
}

void CloseGate(string &in asTimer)
{
	PlayGuiSound("door_level_wood_close.snt", 1.0f);
	AddTimer("ActivatePlayer", 1, "ActivatePlayer");
}

void ActivatePlayer(string &in asTimer)
{
	SetPlayerActive(true);
	ShowPlayerCrossHairIcons(true);
	SetInventoryDisabled(false);
	SetPlayerJumpDisabled(false);
	SetPlayerCrouchDisabled(false);
	FadeIn(1);
	
	SetLocalVarInt("EnableMusic", 1);
	PlayMusic("amb_safe", true, 0.5f, 5, 0, true);
}

//END START GAME//
//////////////////

////////////////////
//BEGIN LARGE ROOM//
/*Bird wings when entering room first time
 */
void CollideEnterLargeRoom(string &in asParent, string &in asChild, int alState)
{
	PlaySoundAtEntity("BirdSound", "general_birds_flee", "BirdSound", 0, false);
	
	AddTimer("whirl", 7, "TimerDustWhirl");

	/*DEBUG
	 */	
	AddDebugMessage("Entered big room, birds flee!", true);
}
/*Random whirls of dust
 */
void TimerDustWhirl(string &in asTimer)
{
	int iWhirl= RandFloat(1, 4);
	float fWhirl= RandFloat(0.5f,15.5f);
	
	CreateParticleSystemAtEntity("whirl"+iWhirl, "ps_dust_whirl.ps", "Whirl0"+iWhirl, false);
	
	PlaySoundAtEntity("whirl"+iWhirl, "general_wind_whirl.snt", "Whirl0"+iWhirl, 0, false);
	
	AddTimer("whirl", 14.5f+fWhirl, "TimerDustWhirl");
	
	/*DEBUG
	 */
	AddDebugMessage("Where Whirl Now: "+iWhirl+" Next Whirl In: "+(14.5f+fWhirl), true);
}
//END LARGE ROOM//
//////////////////

///////////////////////
//START HOLLOW NEEDLE//
void CollideFirstQuest(string &in asParent, string &in asChild, int alState)
{
	AddQuest("HollowNeedleArchives","HollowNeedleArchives");
}
//END HOLLOW NEEDLE//
/////////////////////

//////////////////////
//BEGIN BLOCKED DOOR//
/*Break wood planks to unlock door. ZION: This must be a veeery old quest. Must be from the time where weapons were used to break planks. This probably is a sign for me to take creative liberty when creating entirely new rooms around the castle.
 */
void FuncUnlockDoor(string &in asEntity, string &in asType)
{
	SetSwingDoorLocked("castle_arched01_1", false, true);
	
	/*DEBUG
	 */
	AddDebugMessage("UnlockDoor!", true);
}
//END BLOCKED DOOR//
////////////////////

/////////////////////////////
//BEGIN INTERACT LARGE GATE//
void InteractLargeGate(string &in asEntity)
{
	PlayGuiSound("close_gate.ogg", 0.5f);
}

//END INTERACT LARGE GATE//
///////////////////////////

void InteractLockedDoor(string &in asEntity)
{
	AddTimer("lockeddoor", 0.5f, "TimerLockedDoor4evah");
	
	PlaySoundAtEntity("locked_"+asEntity, "locked_door", asEntity, 0.0f, false);
}

void TimerLockedDoor4evah(string &in asTimer)
{
	SetMessage("LevelDoors", "LockedForever", 0);
}

////////////////////////////
// Run first time starting map
void OnStart()
{
	//----GAME START----//

	FadeGlobalSoundVolume(0, 0.01f);
	FadeGlobalSoundSpeed(0.7, 0.01f);

	SetPlayerActive(false);
	ShowPlayerCrossHairIcons(false);
	SetInventoryDisabled(true);
	SetPlayerJumpDisabled(true);
	SetPlayerCrouchDisabled(true);
	FadeOut(0);
	AddTimer("StartGame", 1, "StartGame");

	//----COLLIDE CALLBACKS----//
	AddEntityCollideCallback("Player", "ScareBirds", "CollideEnterLargeRoom", true, 1);
	AddEntityCollideCallback("Player", "AreaFirstQuest", "CollideFirstQuest", true, 1);
	
	//---- INTERACT INIT ----//
	SetEntityPlayerInteractCallback("castle_1", "PlayerInteractDoor", true);
	SetEntityCustomFocusCrossHair("LargeGate", "LevelDoor");
	
	//----QUEST INIT----//
	//SetNumberOfQuestsInMap(1);
	
	//----DEBUG----//
	if(ScriptDebugOn())
	{
		GiveItemFromFile("lantern", "lantern.ent");
		GiveItemFromFile("chemical_container_1", "chemical_container.ent");
		GiveItemFromFile("chemical_container_2", "chemical_container.ent");
		GiveItemFromFile("Chemical_1", "flask01_aqua_regia.ent");
		GiveItemFromFile("Chemical_2", "flask01_aqua_regia.ent");
		GiveItemFromFile("Chemical_3", "flask01_aqua_regia.ent");
		GiveItemFromFile("Chemical_4", "flask01_aqua_regia.ent");
		
		GiveItemFromFile("key_study_1", "key_study.ent");
		
		//SetGlobalVarString("key_study_1","key_study_1");	//PlayerStartArea_3
		//SetGlobalVarInt("ChemJars",4);	//PlayerStartArea_2
		//SetGlobalVarString("chemical_container_2","chemical_container_2"); //PlayerStartArea_5
	}
}


////////////////////////////
// Run when entering map
void OnEnter()
{
	SetMapDisplayNameEntry("LevelMainChamber");
	if(GetLocalVarInt("EnableMusic") == 1) PlayMusic("amb_safe", true, 0.5f, 5, 0, true);
	else StopMusic(5, 0);

	//----PRELOADING----//
	PreloadSound("general_birds_flee"); PreloadSound("general_wind_whirl"); 

	PreloadParticleSystem("ps_dust_whirl");

	//----AUDIO----//
	SetFogProperties(10, 50, 1, false);
	SetFogColor(0.12f, 0.14f, 0.18f, 1);
	SetFogActive(true);

	//----PLAYER INIT----//
	SetPlayerRunSpeedMul(1);	//Able to run now! Keeping this here just in case.
}

////////////////////////////
// Run when leaving map
void OnLeave()
{
	StopMusic(5, 0);
}